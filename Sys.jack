class Sys {
    function void init() {
        do Memory.init();
        do Math.init();
        do Screen.init();
        do Output.init();
        do Keyboard.init();

        do Main.main();

        do Sys.halt();
        return;
    }

    function void halt() {
        while (true) {}
        return;
    }

    function void error(int errorCode) {
        do Output.printString("ERR");
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }

    function void wait(int duration) {
        var int i, j;

        if (duration < 0) {
            do Sys.error(1);
        }

        let i = 0;
        while (i < duration) {
            let j = 0;
            while (j < 100) {
                let j = j + 1;
            }
            let i = i + 1;
        }
        return;
    }
}
